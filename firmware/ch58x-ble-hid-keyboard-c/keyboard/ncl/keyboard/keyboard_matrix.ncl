let C = import "keyboard/contracts.ncl" in

{
  contracts = {
    Board = {
      matrix
        | doc "the cols/rows, and number of keys, used for generating the matrix scan code."
        | {
          cols | Array C.GpioPin,
          rows | Array C.GpioPin,
          key_count | Number,
          implementation | String | default = "col_to_row",
        },

      keymap_index_for_key,

      ..
    },
  },

  matrix_scan = {
    keyboard_matrix_init,
    keyboard_matrix_scan_raw,
    is_sw_1_1_pressed,
  },

  board
    | C.Board,

  gpio_pins
    | C.GpioPins,

  cmakelists.keyboard_matrix =
    [
      "target_compile_definitions(keyboard INTERFACE KEYBOARD_MATRIX_IMPL_%{board.matrix.implementation |> std.string.uppercase})",
      "target_compile_definitions(keyboard INTERFACE KEYBOARD_MATRIX_KEY_COUNT=%{board.matrix.key_count |> std.to_string})",
      "target_compile_definitions(keyboard INTERFACE KEYBOARD_MATRIX_ROW_COUNT=%{board.matrix.rows |> std.array.length |> std.to_string})",
      "target_compile_definitions(keyboard INTERFACE KEYBOARD_MATRIX_COL_COUNT=%{board.matrix.cols |> std.array.length |> std.to_string})",
    ]
    |> std.string.join "\n",

  sources.keyboard_matrix = m%"
    #include "keyboard_gpio.h"
    // Generated by codegen.
    const keyboard_gpio_t keyboard_matrix_cols[KEYBOARD_MATRIX_COL_COUNT] = {
      %{
        board.matrix.cols
        |> std.array.map (fun { as_keyboard_gpio_expr, .. } => as_keyboard_gpio_expr)
        |> std.string.join ",\n"
      }
    };
    const keyboard_gpio_t keyboard_matrix_rows[KEYBOARD_MATRIX_ROW_COUNT] = {
      %{
        board.matrix.rows
        |> std.array.map (fun { as_keyboard_gpio_expr, .. } => as_keyboard_gpio_expr)
        |> std.string.join ",\n"
      }
    };
    const int16_t keymap_indices[KEYBOARD_MATRIX_ROW_COUNT]
                                [KEYBOARD_MATRIX_COL_COUNT] = {
      %{
        board.matrix.rows
        |> std.array.map_with_index (fun row_idx _ =>
          board.matrix.cols
          |> std.array.map_with_index (fun col_idx _ =>
            board.keymap_index_for_key { row_index = row_idx, column_index = col_idx } |> match {
                'NoKey => "-1",
                'Key index => std.to_string index,
            }
          )
          |> std.string.join ", "
          |> (fun s => "{ %{s} }"))
        |> std.string.join ",\n"
      }
    };
    "%,
}

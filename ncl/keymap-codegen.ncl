{
  serialized_json_keymap
    | doc "The 'JSON serialized' value of the keymap. e.g. imported from keymap.json.",

  simple
    | doc "Generates type declaration and value expression for smart_keymap::key::simple::Key."
    = {
      key_type = "crate::key::simple::Key",
      rust_expr = fun key => "crate::key::simple::Key(%{std.to_string key})",
    },

  tap_hold
    | doc "Generates type declaration and value expression for smart_keymap::key::tap_hold::Key."
    = {
      key_type = "crate::key::tap_hold::Key",
      rust_expr = fun { hold, tap, } =>
        m%"
        crate::key::tap_hold::Key {
          hold: %{std.to_string hold},
          tap: %{std.to_string tap},
        }
        "%,
    },

  keymap_len
    | doc "The keymap length."
    = std.array.length serialized_json_keymap,

  keys_id
    | doc "The identifier for the KeysN tuple struct used in the generated keymap.rs."
    = "Keys%{std.to_string keymap_len}",

  key_type_of_key
    | doc "The type declaration for the key."
    = match {
      { simple = _ } => simple.key_type,
      { tap_hold = _ } => tap_hold.key_type,
      _ => "unknown_key_type"
    },

  rust_expr_of_key
    | doc "The Rust expression for the key."
    = match {
      { simple = key } => simple.rust_expr key,
      { tap_hold = key } => tap_hold.rust_expr key,
      _ => "unknown_key_type"
    },

  key_types
    | doc "The comma-separated list of key types, passed to the KeysN generics."
    =
      serialized_json_keymap
      |> std.array.map key_type_of_key
      |> std.string.join ",",

  key_exprs
    | doc "The comma-separated list of key expressions, passed to the KeysN constructor."
    =
      serialized_json_keymap
      |> std.array.map (fun key => "%{rust_expr_of_key key},")
      |> std.string.join "",

  keymap_rs
    | doc "Text contents of the keymap.rs generated from the keymap.json"
    = m%"
crate::tuples::define_keys!(%{std.to_string keymap_len});

/// Alias for a [tuples] KeysN type, as generated by keymap.ncl.
pub type KeyDefinitionsType = %{keys_id}<
%{key_types}
>;

/// A [tuples] KeysN value with keys, as generated by keymap.ncl.
pub const KEY_DEFINITIONS: KeyDefinitionsType = %{keys_id}::new((
%{key_exprs}
));
"%
}
